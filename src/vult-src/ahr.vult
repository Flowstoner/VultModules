/*
Copyright (c) 2017 Leonardo Laguna Ruiz (modlfo@gmail.com), All rights reserved.

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

1.- Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
2.- Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
3.- Neither the name of the copyright holder nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.
4.- Commercial use requires explicit permission of the author.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

fun do(gate:bool, a:real, h:real, r:real, enabled:bool, loop:bool) {
   mem state;
   mem out;
   mem rate;
   mem target;
   mem hold_phase;

   val release = 0;
   val attack  = 1;
   val hold    = 2;
   val reset   = 3;

   if(Util.edge(gate)) {
      state = reset;
   }

   val resetting = if state == reset || (state == release && out < 0.001) then 1.0 else 0.0;

   // states

   if(state == reset) {
      rate = 1.0 / 0.1;
      target = 0.0;
   }
   if(state == attack && enabled) {
      rate = 1.0 / (100.0 * a + 0.01);
      target = 1.2;
   }
   if(state == hold) {
      val hrate = 1.0 / (100.0 * h + 0.01);
      hold_phase = hold_phase + hrate;
   }
   if(state == release) {
      rate = 1.0 / (200.0 * r + 0.05);
      target = 0.0;
   }

   // transitions

   if(Util.edge(out > 1.0)) {
      hold_phase = 0.0;
      state = hold;
   }
   if(out < 0.001 && state == reset) {
      state = attack;
   }
   if(hold_phase > 100.0 && state == hold) {
      state = release;
   }
   if(loop && gate && state == release && out < 0.001) {
      state = reset;
   }

   out = out + (target - out) * rate * 0.005 * Util.sampleRateScale();

   return clip(out, 0.0, 1.0), resetting;
}

fun loop(gate:bool, a:real, h:real, r:real) {
   mem state;
   mem out;
   mem rate;
   mem target;
   mem hold_phase;
   mem enabled;

   val release = 0;
   val attack  = 1;
   val reset   = 3;

   if(Util.edge(gate)) {
      state = reset;
      hold_phase = 0.0;
      enabled = true;
   }

   val r_current = if enabled then a else r;

   val hrate = 1.0 / (100.0 * h + 0.01);
   hold_phase = hold_phase + hrate;

   if(hold_phase > 1600.0 && out > 0.45 && out < 0.55)
      enabled = false;

   val resetting = if state == reset then 1.0 else 0.0;

   // states

   if(state == reset) {
      rate = 1.0 / 0.1;
      target = 0.0;
   }
   if(state == attack && enabled) {
      rate = 1.0 / (100.0 * a + 0.01);
      target = 1.2;
   }
   if(state == release) {
      rate = 1.0 / (200.0 * r_current + 0.01);
      target = 0.0;
   }

   // transitions

   if(Util.edge(out > 1.0)) {
      state = release;
   }
   if(out < 0.001 && state == reset) {
      state = attack;
   }
   if(state == release && out < 0.001) {
      state = reset;
   }

   out = out + (target - out) * rate * 0.005 * Util.sampleRateScale();

   return clip(out, 0.0, 1.0), resetting;
}
